# https://github.com/dustinlyons/nixos-config/blob/feca83e96a9f2dc4b3a4fa3073e63f6de1d10d72/modules/darwin/dock/default.nix
{ config, pkgs, lib, ... }:
# Original source: https://gist.github.com/antifuchs/10138c4d838a63c0a05e725ccd7bccdd

with lib;
let
  cfg = config.my.dock;
  inherit (pkgs) stdenv dockutil killall;
in
{
  options = {
    my.dock = {
      enable = mkOption {
        description = "Enable dock";
        default     = stdenv.isDarwin;
      };

      entries = mkOption {
        description = "Entries on the Dock";
        type =
          with types;
          listOf (submodule {
            options = {
              path    = lib.mkOption { type = str; };
              section = lib.mkOption {
                type    = str;
                default = "apps";
              };
              options = lib.mkOption {
                type    = str;
                default = "";
              };
            };
          });
        readOnly = true;
      };

      username = mkOption {
        description = "Username to apply the dock settings to";
        type        = types.str;
      };
    };
  };

  config = mkIf cfg.enable (
    let
      normalize = path: if hasSuffix ".app" path then path + "/" else path;
      entryURI =
        path:
        "file://"
        + (builtins.replaceStrings
          [ " " "!" "\"" "#" "$" "%" "&" "'" "(" ")" ]
          [ "%20" "%21" "%22" "%23" "%24" "%25" "%26" "%27" "%28" "%29" ]
          (normalize path)
        );
      wantURIs    = concatMapStrings (entry: "${entryURI entry.path}\n") cfg.entries;
      createEntries =
        concatMapStrings
          (entry:
            "${dockutil}/bin/dockutil --no-restart --add '${entry.path}' --section ${entry.section} ${entry.options}\n"
          )
          cfg.entries;
    in
    {
      home.activation.buildDock = lib.hm.dag.entryAfter ["installPackages"] /* bash */ ''
        run echo >&2 "Setting up the Dock for ${cfg.username}..."
        #su ${cfg.username} -s /bin/sh <<'USERBLOCK'
        haveURIs="$(${dockutil}/bin/dockutil --list | ${pkgs.coreutils}/bin/cut -f2)"
        if ! diff -wu <(echo -n "$haveURIs") <(echo -n '${wantURIs}') >&2 ; then
          run echo >&2 "Resetting Dock."
          run ${dockutil}/bin/dockutil --no-restart --remove all
          run ${createEntries}
          run ${killall}/bin/killall -KILL Dock
        else
          run echo >&2 "Dock setup complete."
        fi
        #USERBLOCK
      '';
    }
  );
}
